gulp = require('gulp')
gulpWebpack = require('gulp-webpack')
gutil = require('gulp-util')
plugins = require('gulp-load-plugins')()
runSequence = require('run-sequence')
coffeelint = require('gulp-coffeelint')
serverConfig = require('./config/server.config')
spawn = require('child_process').spawn

WebpackDevServer = require('webpack-dev-server')
makeHotDevWebPackConfig = require('./WebPackConfigCreator').makeHotDevWebPackConfig
makeServerSideWebPackConfig = require('./WebPackConfigCreator').makeServerSideWebPackConfig
makeClientSideWebPackConfig = require('./WebPackConfigCreator').makeClientSideWebPackConfig
webpack = require('webpack')

ENV = process.env.NODE_ENV || 'DEV'

{ config } = require('rygr-util')
config.initialize('config/*.json')

# removes all server deployment assets
gulp.task 'clean', ->
  gulp.src("#{ serverConfig.deployDir }assets/*", { read: false }).pipe plugins.clean()

# there is an additional lint in the webpack
# this is for files not packed (like this gulpfile)
gulp.task 'coffeelint', ->
  gulp.src('./*.coffee')
    .pipe(coffeelint('config/coffeelintrc.json'))
    .pipe(coffeelint.reporter())

# implicitly renders the client js and serves it via a hot reloader
gulp.task 'webpack-hot-reload-server', (cb) ->
  webpackConfig = makeHotDevWebPackConfig()
  compiler = webpack(webpackConfig)
  DEV_PORT = config.server.port
  # Start a webpack-hot-reload-server
  new WebpackDevServer(compiler, {
    hot: true,
    publicPath: '/assets/',
    historyApiFallback: true,
    stats: { colors: true },
    contentBase: './src/',
  }).listen DEV_PORT, 'localhost', (err) ->
    if err
      throw new (gutil.PluginError)('webpack-hot-reload-server', err)
    plugins.util.log("ðŸŽº  ðŸŽº  ðŸŽº  ðŸŽº    Webpack server started    ðŸŽ·  ðŸŽ·  ðŸŽ·  ðŸŽ·")
    plugins.util.log("ðŸŽ¬  ðŸŽ¬  ðŸŽ¬  ðŸŽ¬    Compiling client bundle   ðŸŽ¬  ðŸŽ¬  ðŸŽ¬  ðŸŽ¬")
  cb()

# Runs a hot reloaded express server with server-side rendering
gulp.task 'webpack-dev-server', (cb) ->
  webpackConfig = makeServerSideWebPackConfig('DEV', true)
  webpack webpackConfig, (err, stats) ->
    if err
      throw new (gutil.PluginError)('webpack:build', err)
  cb()

# Starts the express server created by the 'webpack-dev-server' task
gulp.task 'node-server', (cb) ->
  env = Object.create(process.env)
  env.NODE_ENV = ENV

  child = spawn('node', ['deploy/index.js'], { cwd: process.cwd(), env: env })

  child.stdout.setEncoding('utf8')
  child.stdout.on 'data', (data) ->
    gutil.log(data)

  child.stderr.setEncoding('utf8')
  child.stderr.on 'data', (data) ->
    gutil.log(gutil.colors.red(data))
    gutil.beep()

# Creates production bundles, one for server (render.js) one for client (main.js)
gulp.task 'build-bundles', (cb) ->
  config = [makeServerSideWebPackConfig(ENV), makeClientSideWebPackConfig(ENV)]
  webpack config, (err, stats) ->
    if err
      throw new (gutil.PluginError)('webpack:build', err)
    gutil.log('[webpack:build]', stats.toString({ colors: true }))
    cb()


# ------------------------------------------------------------------------------
# Sequences
# ------------------------------------------------------------------------------

gulp.task 'dev', (cb) ->
  runSequence('clean', ['webpack-dev-server', 'webpack-hot-reload-server', 'node-server'], cb)

# ------------------------------------------------------------------------------
# Default
# ------------------------------------------------------------------------------
#
gulp.task 'default', ->
  runSequence('dev')
